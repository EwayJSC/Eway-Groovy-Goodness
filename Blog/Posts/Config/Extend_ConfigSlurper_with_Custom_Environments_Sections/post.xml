tag:blogger.com,1999:blog-6671019398434141469.post-40002649845003828472014-05-05T17:03:00.000+02:002014-05-05T17:03:02.904+02:00Groovy Goodness: Extend ConfigSlurper with Custom Environments Sections<p>Groovy contains the useful <a href="http://mrhaki.blogspot.com/2009/10/groovy-goodness-using-configslurper.html">ConfigSlurper</a> for reading in configuration files where settings can be different for different environments. We only have to include an <code>environments</code> block and define values for configuration properties per environment. Since Groovy 2.3 we can add our own conditional configuration blocks and therefore don't have to put everything in an <code>environments</code> block.</p><p>In the following sample we define our own configuration block <code>servers</code> and use two environments <em>local</em> and <code>prod</code>. We given the property <code>mail.host</code> different values per environment:</p><pre class="brush:groovy">// Configuration script.
def config = '''
// Custom block with setting
// conditional per environment.
servers {
    local {
        mail.host = 'greenmail'
    }

    prod {
        mail.host = 'mail.server'
    }
}

environments {
    local {
        appName = 'local'
    }
    prod {
        appName = 'production'
    }
}
'''

// Helper closure to create a new
// ConfigSlurper for the given environment and
// register servers as section with configuration
// per environment.
def createConfig = { env ->
    def configSlurper = new ConfigSlurper(env)
    configSlurper.registerConditionalBlock('servers', env)
    configSlurper
}

// Create configuration slurper and
// set environment to prod.
def configuration = createConfig('prod').parse(config)

assert configuration.mail.host == 'mail.server'
assert configuration.appName == 'production'


// Create configuration slurper and
// set environment to local.
configuration = createConfig('local').parse(config)

assert configuration.mail.host == 'greenmail'
assert configuration.appName == 'local'
</pre><p>Code written with Groovy 2.3.</p>Hubert Klein Ikkinkhttps://plus.google.com/116304108087638220732noreply@blogger.com0