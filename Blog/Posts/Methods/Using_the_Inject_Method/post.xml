tag:blogger.com,1999:blog-6671019398434141469.post-89019533290227688232009-09-06T07:00:00.003+02:002010-11-26T11:15:13.258+01:00Groovy Goodness: Using the Inject Method<p>Groovy has some features and methods we can categorize as functional programming. The <code>inject()</code> method is a so called higher-order function. Other languages call it a fold, reduce or accumulate. The <code>inject()</code> method processes a data structure with a closure and builds up a return value. The first parameter of the <code>inject()</code> method is the first value of the intermediary results of the second parameter: the closure. When we use the <code>inject()</code> we don't introduce any side effects, because we build up the value without using any <em>outside</em> variable.</p>
<p>To understand the <code>inject()</code> method better we look at some sample code:</p>
<pre class="brush:groovy">
// Traditional "sum of the values in a list" sample.
// First with each() and side effect, because we have
// to declare a variable to hold the result:
def total = 0
(1..4).each { total += it }
assert 10  == total

// With the inject method we 'inject' the 
// first value of the result, and then for
// each item the result is increased and
// returned for the next iteration.
def sum = (1..4).inject(0) { result, i -> result + i }
assert 10 == sum

// We add a println statement to see what happens.
(1..4).inject(0) { result, i ->
    println "$result + $i = ${result + i}"
    result + i
}
// Output: 
// 0 + 1 = 1
// 1 + 2 = 3
// 3 + 3 = 6
// 6 + 4 = 10


class Person {
    String username
    String email
}
def persons = [
    new Person(username:'mrhaki', email: 'email@host.com'),
    new Person(username:'hubert', email: 'other@host.com')
]

// Convert list to a map where the key is the value of
// username property of Person and the value is the email
// property of Person. We inject an empty map as the starting
// point for the result.
def map = persons.inject([:]) { result, person ->
    result[person.username] = person.email
    result
}
assert [mrhaki: 'email@host.com', hubert: 'other@host.com'] == map
</pre>
<p>Run this script on <a href="http://groovyconsole.appspot.com/?id=12002">GroovyConsole</a>.</p>Hubert Klein Ikkinkhttps://plus.google.com/116304108087638220732noreply@blogger.com3